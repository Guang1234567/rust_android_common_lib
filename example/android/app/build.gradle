apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.rust.example.android"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
                abiFilters 'armeabi-v7a', 'x86_64', 'arm64-v8a'
            }
        }
    }
    signingConfigs {
        release {
            storeFile file("android_example_keystore.jks")
            storePassword "1234567"
            keyAlias "key0"
            keyPassword "1234567"
        }
    }
    buildTypes {
        release {
            debuggable true
            jniDebuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }

        debug {
            debuggable true
            jniDebuggable true
            applicationIdSuffix ".debug"
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs += ['libs']
        }
    }

    packagingOptions {
        doNotStrip "**/*.so"
    }

    flavorDimensions 'cpuArch'
    productFlavors {
        arm7 {
            dimension 'cpuArch'
            ndk {
                abiFilters 'armeabi-v7a'
            }
        }
        arm8 {
            dimension 'cpuArch'
            ndk {
                abiFilters 'arm64-v8a'
            }
        }
        x86_64 {
            dimension 'cpuArch'
            ndk {
                abiFilters 'x86_64'
            }
        }
        universal {
            dimension 'cpuArch'
            // include all default ABIs. with NDK-r14, they are:
            //   armeabi-v7a, arm64-v8a, x86_64
        }
    }

    def buildType = "release"
    applicationVariants.all { variant ->
        buildType = variant.buildType.name // Sets the current build type
    }

    //copying so files

    task compileNativeLib(type: Exec) {
        doFirst {
            println("\n============================== Start Compile Native Lib ==============================")
            println("buildType = ${buildType}")
            workingDir "./../../.."
            if (buildType.equalsIgnoreCase("debug")) {
                commandLine "./build_for_android.sh"
            } else {
                commandLine "./build_for_android.sh", "--${buildType}"
            }
        }
        doLast {
            println("\n============================== Finish Compile Native Lib ==============================\n")
        }
    }

    task copyArm8NativeLib() {
        doLast {
            copy {
                from "./../../../target/aarch64-linux-android/${buildType}/libgreetings.so"
                into "src/main/jniLibs/arm64-v8a"
            }
        }
    }

    task copyArm7NativeLib() {
        doLast {
            copy {
                from "./../../../target/armv7-linux-androideabi/${buildType}/libgreetings.so"
                into "src/main/jniLibs/armeabi-v7a"
            }
        }
    }

    task copyX86_64NativeLib() {
        doLast {
            copy {
                from "./../../../target/x86_64-linux-android/${buildType}/libgreetings.so"
                into "src/main/jniLibs/x86_64"
            }
        }
    }

    task copyNativeLib() {

        dependsOn compileNativeLib

        dependsOn copyArm8NativeLib
        dependsOn copyArm7NativeLib
        dependsOn copyX86_64NativeLib
    }

    //add dependencies
    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn copyNativeLib
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
